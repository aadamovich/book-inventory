
import com.google.zxing.BarcodeFormat
import com.google.zxing.client.j2se.MatrixToImageWriter
import com.google.zxing.common.BitMatrix
import com.google.zxing.qrcode.QRCodeWriter
import com.google.zxing.EncodeHintType

import org.apache.batik.transcoder.image.JPEGTranscoder
import org.apache.batik.transcoder.TranscoderInput
import org.apache.batik.transcoder.TranscoderOutput

import groovy.util.XmlSlurper
import groovy.xml.XmlUtil

import java.awt.image.BufferedImage
import java.awt.Graphics2D
import java.awt.Color

import static javax.imageio.ImageIO.*

apply plugin: 'base'

buildscript {
  repositories { mavenCentral() }
  dependencies {
    classpath 'com.google.zxing:core:3.2.0'
    classpath 'com.google.zxing:javase:3.2.0'
    classpath('batik:batik-transcoder:1.6-1') {
      exclude module: 'fop'
    }
    classpath 'xerces:xercesImpl:2.11.0'
  }
}


task generateBadges { doLast {
  buildDir.mkdirs()
  File svgTemplate = file('badge_template.svg')
  int fileIndex = 1
  forEachBook { bookName ->
    logger.quiet "> Generating badge for ${bookName}"
    String indexString = (fileIndex++).toString().padLeft(3, '0')
    String baseName = "${indexString}"
    File svgBadgeFile = file("${buildDir}/${baseName}_badge.svg")    
    File qrFile = renderQRCodeImage("mailto:andrey.adamovich@gmail.com", buildDir, baseName)
    svgBadgeFile.text = modifySVG(svgTemplate.text, bookName, qrFile)
    renderBadgeImage(svgBadgeFile, buildDir, baseName)
    qrFile.delete()
    svgBadgeFile.delete()
  }
}}

generateBadges.logging.captureStandardOutput LogLevel.INFO
generateBadges.logging.captureStandardError LogLevel.INFO


task concatenateBadges(dependsOn: generateBadges) { doLast {
  File pageDir = file("${buildDir}/pages")
  pageDir.mkdirs()
  int badgesPerPage = 16
  int badgeHeight = 408
  int badgeWidth = 1190
  int badgeMargin = 6
  int startX = 50
  int startY = 70
  buildDir.listFiles().findAll { it.name.endsWith('.jpg') }.collate(badgesPerPage).eachWithIndex { badgeGroup, groupIndex ->
    BufferedImage page = read(new File('a4.jpg'))
    Graphics2D pageGraphics = page.createGraphics()
    pageGraphics.setColor(Color.LIGHT_GRAY)
    println "> Generating page ${groupIndex}"
    badgeGroup.eachWithIndex { File badgeFile, imageIndex ->
      BufferedImage badgeImage = read(badgeFile)
      println "> Adding ${imageIndex}:${badgeFile.name} to page"
      int x = (imageIndex % 2) * (badgeWidth + badgeMargin * 2) + startX
      int y = ((int)(imageIndex / 2)) * (badgeHeight + badgeMargin * 2) + startY
      pageGraphics.drawImage(badgeImage, null, x, y)
      if (project.hasProperty('drawLines')) {
        pageGraphics.drawLine(0, y, Integer.MAX_VALUE, y)
        pageGraphics.drawLine(0, y + badgeHeight, Integer.MAX_VALUE, y + badgeHeight)
        pageGraphics.drawLine(x, 0, x, Integer.MAX_VALUE)
        pageGraphics.drawLine(x + badgeWidth, 0, x + badgeWidth, Integer.MAX_VALUE)
      }
    }
    write(page, "JPG", new File(pageDir, "page${groupIndex.toString().padLeft(3, '0')}.jpg"))  
  }
}}

concatenateBadges.mustRunAfter generateBadges

build.dependsOn([generateBadges, concatenateBadges])

def modifySVG(String svgText, String bookName, File qrFile) {
  def svg = new XmlSlurper().parseText(svgText)
  def badgeName = svg.depthFirst().find { it.@id == 'badge-first-name' }
  badgeName.replaceBody(sanitizeName(bookName))
  svg.depthFirst().find { it.@id == 'badge-qr' }.@'xlink:href' = "data:image/png;base64,${qrFile.bytes.encodeBase64().toString().toList().collate(76)*.join().join(' ')}".toString()
  XmlUtil.serialize(svg)
}


String sanitizeName(String name) {
  name.
    trim().
    split('\\s+').collect { 
      it.
        trim().
        capitalize() 
    }.
    join(' ')
}


String sanitizeCompany(String company) {
  sanitizeName(
    company.
      replaceAll('-', '').
      trim().
      capitalize()
  )
}


def renderBadgeImage(File svgFile, File baseDir, String baseName) {
  JPEGTranscoder t = new JPEGTranscoder()
  t.addTranscodingHint(JPEGTranscoder.KEY_QUALITY, new Float(1))
  String svgURI = svgFile.toURL().toString()
  t.transcode(new TranscoderInput(svgURI), new TranscoderOutput(new FileOutputStream("${baseDir}/${baseName}_badge.jpg")))
}


File renderQRCodeImage(String content, baseDir, baseName, width = 300, height = 300) {
  File targetFile = file("${baseDir}/${baseName}_qr.png")
  def hints = new EnumMap<EncodeHintType, Object>(EncodeHintType)
  hints.put(EncodeHintType.CHARACTER_SET, "UTF-8")
  hints.put(EncodeHintType.MARGIN, 0)
  BitMatrix bitMatrix = new QRCodeWriter().encode(content, BarcodeFormat.QR_CODE, width, height, hints)
  MatrixToImageWriter.writeToFile(bitMatrix, "png", targetFile)
  targetFile
}


def forEachBook(Closure cl) {
  def lines = file(project.hasProperty('dataFile') ? project.dataFile : 'books.list').readLines().collect { it.trim() }.findAll { it }
  lines.each { String line ->
    cl(line)
  }
}




